{-# LANGUAGE Trustworthy, BangPatterns #-}

module Zero.Zero where

import Zero.Color
-- import Zero.Draw
import Debug.Trace ( trace )
import Data.List
import Data.Char
import Data.Set (Set)
import qualified Data.Set as Set
import Data.Map.Strict (Map)
import qualified Data.Map.Strict as Map

echo :: Show a => String -> a -> IO ()
echo s x = do
   putStr $ clr Cyan $ s <> ": "
   print x
   putStrLn []

-- trace
infix 1 #
(#) :: a -> String -> a
(#) a s = trace (clr Bold $ clr Blue $ "# " ++ s) a

size :: (Foldable t, Enum i, Num i) => t a -> i
size = foldl' (flip $ const succ) 0

-- full range of bounded set
total :: (Bounded a,Enum a) => [a]
total = [minBound..]

-- cycle through enums
next :: (Eq a, Enum a, Bounded a) => a -> a
next a = if a == maxBound then minBound else succ a

prev :: (Eq a, Enum a, Bounded a) => a -> a
prev a = if a == minBound then maxBound else pred a

-- parse numbers from a string
parseNums :: (Read a,Num a) => String -> [a]
parseNums xs
      | null n = []
      | otherwise = read n : parseNums r
      where
      (n,r) = span isDigit $ dropWhile (not . isDigit) xs

-- remove duplicates
unique :: Ord a => [a] -> [a]
unique = Set.toList . Set.fromList

-- count each element's occurrence
index :: forall f a n. (Foldable f, Ord a, Num n) => f a -> Map a n
index = foldl' go Map.empty
   where
   go :: Map a n -> a -> Map a n
   go m x = Map.insertWith (+) x 1 m

-- last n elements
takeLast :: Int -> [a] -> [a]
takeLast n l = go (drop n l) l
   where
   go [] rs = rs
   go xs ys = go (tail xs) (tail ys)

-- get chunks between prefix and suffix, delimited
insides :: (a -> Bool) -> (a -> Bool) -> a -> [a] -> [a]
insides ps pe d = go False
   where
   go _ [] = []
   go False (x:xs) = go (ps x) xs
   go True (x:xs)
      | pe x = d : go False xs
      | otherwise = x : go True xs

-- fixed point of iteration
fixp :: Eq a => (a -> a) -> a -> a
fixp f !a
   | a == a' = a
   | otherwise = fixp f a'
   where
   a' = f a

-- remove element from assoc by key
remove :: Eq k => k -> [(k,v)] -> [(k,v)]
remove _ [] = []
remove k (x:xs)
   | k == fst x = remove k xs
   | otherwise = x : remove k xs

-- remove element from assoc by value
erase :: Eq v => v -> [(k,v)] -> [(k,v)]
erase _ [] = []
erase v (x:xs)
   | v == snd x = erase v xs
   | otherwise = x : erase v xs

-- tortoise and hare cycle finding
floyd :: Eq a => [a] -> Maybe (Integer,a)
floyd [] = Nothing
floyd (a:as) = go 1 1 a as
   where
   go _ _ _ [] = Nothing
   go pow lam x (y:ys)
      | x == y     = Just (lam,x)
      | pow == lam = go (2*pow) 1 y ys
      | otherwise  = go pow (1+lam) x ys

-- simple plot from list of ints
-- plot :: String -> [Int] -> IO ()
-- plot f = draw f . go 0
--    where
--    go _ [] = []
--    go n (x:xs) = [((n,y),True) | y <- [0..x]] ++ go (succ n) xs

-- test
tept :: Show a => String -> (a -> Bool) -> a -> IO ()
tept t p a = putStrLn $ unwords ['\n' : t , clr c m , '\n' : clr c (show a) , "\n"]
   where
   (c,m)
      | p a = (Green,"v")
      | otherwise = (Red,"x")

teqt :: (Eq a,Show a) => String -> a -> a -> IO ()
teqt t e a = putStrLn $ unwords ['\n' : t , clr c m , r , '\n' : clr c (show a) , "\n"]
   where
   (c,m,r)
      | a == e = (Green,"v","")
      | otherwise = (Red,"x",'\n' : show e)

-- set
(∈) :: (Foldable t, Eq a) => a -> t a -> Bool
(∈) = elem

(∉) :: (Foldable t, Eq a) => a -> t a -> Bool
(∉) = notElem

(∋) :: (Foldable t, Eq a) => t a -> a -> Bool
(∋) = flip elem

class Comparable s where
   (∪) :: s -> s -> s
   (∩) :: s -> s -> s

instance Eq a => Comparable [a] where
   (∪) = union
   (∩) = intersect

instance Ord a => Comparable (Set a) where
   (∪) = Set.union
   (∩) = Set.intersection
